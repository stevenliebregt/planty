plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
    id 'org.openjfx.javafxplugin' version "$javafxplugin_version"
}

group 'com.stevenliebregt'
version '1.0-SNAPSHOT'

mainClassName = 'com.stevenliebregt.planty.MainKt'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // https://mvnrepository.com/artifact/net.sourceforge.plantuml/plantuml
    implementation group: 'net.sourceforge.plantuml', name: 'plantuml', version: "$plantuml_version"

    // https://mvnrepository.com/artifact/no.tornado/tornadofx
    implementation group: 'no.tornado', name: 'tornadofx', version: "$tornadofx_version"

    // https://mvnrepository.com/artifact/org.fxmisc.richtext/richtextfx
    implementation group: 'org.fxmisc.richtext', name: 'richtextfx', version: "$richtextfx_version"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
}

javafx {
    version = '14'
    modules = ['javafx.controls', 'javafx.graphics', 'javafx.web']
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

tasks {
    test {
        useJUnitPlatform()

        testLogging {
            events = [
                    'started',
                    'passed',
                    'failed'
            ]

            // show standard out and standard error of the test
            // JVM(s) on the console
            showStandardStreams = true
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = '11'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '11'
}